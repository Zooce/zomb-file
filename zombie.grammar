------------------------------------------------------------------------- Legend

+X     =>  1 or more of X
*X     =>  0 or more of X
*      =>  0 or more of anything
?X     =>  0 or 1 of X
( .. ) =>  group - treated as a single entity
X | Y  =>  X or Y
X - Y  =>  X excluding Y
'X'    =>  'X' single character literal
.X     =>  .X multi-character literal
X %    =>  X is recursive


----------------------------------------------------------------- Syntax Grammar
-- NOTE: all white space (' ' | '\t' | newline) is ignored between entities unless otherwise noted

zombie-file
    +decl .Eof

decl
    kv-pair | macro-decl

kv-pair
    key '=' value newline

value
    string | number | object | array | macro-use

object %
    '{' *kv-pair '}'

array %
    '[' ?( value *( ',' value ) ?',' ) ']'

macro-decl
    macro-key ?macro-decl-params '=' value newline

macro-key
    '$' key

macro-decl-params
    '(' key *( ',' key ) ?',' ')'

macro-use %
    macro-key ?macro-use-params *key-accessor

macro-use-params %
    '(' param *( ',' param ) ?',' ')'

param
    key | macro-use

key-accessor
    '.' key | '[' range ']'

range (always inclusive)
    ?number '..' ?number

key
    string | number

string
    bare_string | quoted_string

bare_string
    +( character - delimiter )

quoted_string
    '"' escaped_string '"'

escaped_string
    *( *( character - escape ) *( '\' escape ) )

character
    0x20...0x10FFFF

number
    0 | 1-9 ( 0-9 )*

escape
    '"' | '\' | 'b' | 'f' | 'n' | 'r' | 't' | 'u' hex hex hex hex

hex
    0-9 | 'A'...'F' | 'a'...'f'

delimiter
    '\t'  // 0x09 .Tab
    '\n'  // 0x0A .LineFeed
    '\r'  // 0x0D .CarriageReturn
    ' '   // 0x20 .Space
    '"'   // 0x22 .Quote
    '$'   // 0x24 .Dollar
    '('   // 0x28 .OpenParen
    ')'   // 0x29 .CloseParen
    ','   // 0x2C .Comma
    '.'   // 0x2E .Dot
    '='   // 0x3D .Equals
    '['   // 0x5B .OpenSquare
    '\'   // 0x5C .ReverseSolidus
    ']'   // 0x5D .CloseSquare
    '{'   // 0x7B .OpenCurly
    '}'   // 0x7D .CloseCurly

newline
    '\n' | '\r\n'


-- NOTE: comments are special because they can be placed anywhere as long as
         no other important token is on the rest of the line
comment
    '//' * newline


------------------------------------------------------- Lexical Grammar (Tokens)

.CloseCurly = '}'
.CloseParen = ')'
.CloseSquare = ']'
.Comma = ','
.Dollar = '$'
.Dot = '.'
.Equals = '='
.OpenCurly = '{'
.OpenParen = '('
.OpenSquare = '['

.Range = '..'
.Comment = comment
.Newline = newline
.Number = number
.String = string

.Eof = end of the file
