;; The ZOMB file format grammar in ABNF format.

zomb = decl *( separator decl ) [ separator ]

decl = kv-pair / macro-def / comment

;; Key-Value Pairs

kv-pair = string equals value

value = string / ml-string / object / array / macro-expr

object = object-begin [ kv-pair-list ] object-end

kv-pair-list = kv-pair *( separator kv-pair ) [ separator ]

array = array-begin [ value-list ] array-end

value-list = value *( separator value ) [ separator ]

macro-expr = macro-key [ macro-expr-params ] *( macro-accessor )

macro-expr-params = macro-params-begin value-list macro-params-end

macro-accessor = %x2E string

;; Macros

macro-def = macro-key [ macro-def-params ] equals macro-value

macro-key = %x24 string

macro-def-params = macro-params-begin string-list macro-params-end

string-list = string *( separator string ) [ separator ]

macro-value = macro-param / string / ml-string / macro-object / macro-array / macro-macro-expr

macro-param = %x25 string

macro-object = object-begin [ macro-kv-pair-list ] object-end

macro-kv-pair = string equals macro-value

macro-kv-pair-list = macro-kv-pair *( separator macro-kv-pair ) [ separator ]

macro-array = array-begin [ macro-value-list ] array-end

macro-value-list = macro-value *( separator macro-value ) [ separator ]

macro-macro-expr = macro-key [ macro-macro-expr-params ] *( macro-accessor )

macro-macro-expr-params = macro-params-begin macro-value-list macro-params-end

;; Strings

string = bare-string / quoted-string

bare-string = 1*( non-delimiter )

quoted-string = DQUOTE escaped-string DQUOTE

escaped-string = *( non-escaped / ( %x5C escaped ) )

ml-string = 1*( [ padding ] %x5C.5C *any newline )

;; Comment

comment = %x2F.2F *any newline

;; Padded Delimiters

equals = [ padding ] %x3D [ padding ]

comma = [ padding ] %x2C [ padding ]

object-begin = [ padding ] %x7B [ padding ]

object-end = [ padding ] %x7D [ padding ]

array-begin = [ padding ] %x5B [ padding ]

array-end = [ padding ] %x5D [ padding ]

macro-params-begin = [ padding ] %x28 [ padding ]

macro-params-end = [ padding ] %x29 [ padding ]

;; Delimiters

newline =  LF / CRLF

padding = 1*( WSP / newline )

separator = comma / padding

delimiter = WSP / newline
delimiter =/ DQUOTE     ; "
delimiter =/ %x24-25    ; $ and %
delimiter =/ %x28-29    ; ( and )
delimiter =/ %x2C       ; ,
delimiter =/ %x2E       ; .
delimiter =/ %x3D       ; =
delimiter =/ %x5B-5D    ; [ and \ and ]
delimiter =/ %x7B       ; {
delimiter =/ %x7D       ; }

non-delimiter = %x21 / %x23 / %x26-27 / %x2A-2B / %x2D / %x2F-3C / %x3E-5A / %x5E-7A / %x7C / %x7E-10FFFF

escaped = DQUOTE        ; "
escaped =/ %x5C         ; \
escaped =/ %x62         ; b
escaped =/ %x66         ; f
escaped =/ %x6E         ; n
escaped =/ %x72         ; r
escaped =/ %x74         ; t
escaped =/ %x75 4HEXDIG ; uXXXX

non-escaped = %x21 / %x23-5B / %x5D-10FFFF

any = WSP / %x21-10FFFF
